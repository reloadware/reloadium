plugins {
    id('java')
    id('org.jetbrains.intellij') version '1.13.0'
    id("org.jetbrains.kotlin.jvm") version "1.8.10"
    id('com.adarshr.test-logger') version '3.1.0'
}


group 'com.reloadware'
version '1.5.1' //  # RwRender: version '{{ ctx.version }}' //


repositories {
    mavenCentral()
    maven {
        url "https://www.jetbrains.com/intellij-repository/releases"
    }
    maven {
        url "https://www.jetbrains.com/intellij-repository/snapshots"
    }
    maven { url "https://cache-redirector.jetbrains.com/intellij-dependencies" }
    maven {
        url 'https://repository.jboss.org/nexus/content/repositories/snapshots'
    }
    maven {
        url 'https://repository.jboss.org/nexus/content/groups/public'
    }
    maven {
        url 'https://packages.jetbrains.team/maven/p/ij/intellij-dependencies'
    }
}

sourceCompatibility=17
def remoteRobotVersion = "0.11.16"
def intellijVersion = "2023.3.3" //  # RwRender: def intellijVersion = "{{ ctx.intellij_version }}" //
def intellijPro = false  //  # RwRender: def intellijPro = {{ ctx.pro }}  //


dependencies {
    implementation('io.sentry:sentry:6.7.0') {
        exclude group: 'org.slf4j'
    }

    implementation(
            'net.lingala.zip4j:zip4j:2.11.2',
            'commons-io:commons-io:2.11.0',
            "ee.carlrobert:openai-client:1.2.1",
            'com.knuddels:jtokkit:0.1.0',
            'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2',
            'com.vladsch.flexmark:flexmark-all:0.64.8',
    )

    testImplementation(
            'com.intellij.remoterobot:remote-robot:' + remoteRobotVersion,
            'com.intellij.remoterobot:remote-fixtures:' + remoteRobotVersion,
            "com.redhat.devtools.intellij:intellij-common:1.1.0",
            'org.junit.jupiter:junit-jupiter-api:5.9.0',
            'com.redhat.devtools.intellij:intellij-common-ui-test-library:0.2.0',
            'com.automation-remarks:video-recorder-junit5:2.0',
            'org.mock-server:mockserver-netty:5.14.0',
            'org.assertj:assertj-core:3.23.1',
            'org.powermock:powermock-core:2.0.9',
            'org.mockito:mockito-inline:4.8.0',
            'org.mockito:mockito-junit-jupiter:4.8.0',
            'org.junit-pioneer:junit-pioneer:1.9.0',
            'org.skyscreamer:jsonassert:1.5.1',
    )
    testRuntimeOnly(
            'org.junit.jupiter:junit-jupiter-engine:5.9.0'
    )
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resources'
        }
    }
    test {
        java {
            srcDir 'tests/java'
        }
        kotlin {
            srcDir 'tests/kotlin'
        }
        resources {
            srcDir 'tests/resources'
        }
    }
}

intellij {
    if (intellijPro) {
        version = 'PY-' + intellijVersion
        type = 'PY'
        plugins = ["python"]
    } else {
        version = intellijVersion
        type = "PC"
        plugins = ["PythonCore"]
    }
    downloadSources = true
    updateSinceUntilBuild = false
}

test {
    useJUnitPlatform()
}

downloadRobotServerPlugin {
    version.set(remoteRobotVersion)
}

runIdeForUiTests {
    systemProperty "robot-server.port", "8082"
    systemProperty "ide.mac.message.dialogs.as.sheets", "false"
    systemProperty "jb.privacy.policy.text", "<!--999.999-->"
    systemProperty "jb.consents.confirmation.enabled", "false"
    systemProperty "ide.mac.file.chooser.native", "false"
    systemProperty "apple.laf.useScreenMenuBar", "false"
    systemProperty "idea.trust.all.projects", "true"
    systemProperty "ide.show.tips.on.startup.default.value", "false"
}

publishPlugin {
    token = System.getenv("PYCHARM_HUBTOKEN")
}

runPluginVerifier {
    ideVersions = ["IU-231.8770.17", "IU-223.8836.14", "IU-222.4345.14", "IU-233.11555.11", "IU-241.14494.240"]
}

tasks.processResources {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

runIde {
    jvmArgs '-Xmx8G'
    intellij {
        autoReloadPlugins = true
    }
    maxHeapSize = "8g"
}
